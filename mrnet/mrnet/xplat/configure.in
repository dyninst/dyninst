#/****************************************************************************
# * Copyright © 2003-2005 Dorian C. Arnold, Philip C. Roth, Barton P. Miller *
# *                  Detailed MRNet usage rights in "LICENSE" file.          *
# ****************************************************************************/
# $Id: configure.in,v 1.7 2005/04/04 03:56:49 darnold Exp $
#
#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT([xplat], [0.1], [paradyn@cs.wisc.edu])
AC_CONFIG_SRCDIR([src/Process.C])
AC_CONFIG_HEADER([config.h])

AC_CANONICAL_HOST

abs_srcdir=`cd $srcdir && pwd`

dnl === CONFIGURE OPTIONS
AC_ARG_ENABLE(verbosebuild,
    [  --enable-verbosebuild                Display commands during build],
    [if test x"$enableval" = x"no"
     then VERBOSEMAKE=".SILENT:"
     else VERBOSEMAKE=""
     fi],
    [VERBOSEMAKE=".SILENT:"])
AC_SUBST(VERBOSEMAKE)

# Checks for programs.
AC_PROG_CXX(xlC_r CC g++)
CXXFLAGS="$CXXFLAGS -D__STDC_LIMIT_MACROS -D__STDC_CONSTANT_MACROS"

AC_MSG_CHECKING(whether we can compile/link with \"$CXX $CXXFLAGS $LIBS\".)
AC_LINK_IFELSE( [AC_LANG_PROGRAM([],[])],
                [AC_MSG_RESULT(yes)],
                [AC_MSG_ERROR(no)])
AC_SUBST(CXXFLAGS)

AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PROG_INSTALL

ARFLAGS="crv"
AC_SUBST(ARFLAGS)
AC_PATH_PROG(AR, ar)
AC_SUBST(AR)

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([arpa/inet.h fcntl.h limits.h netdb.h netinet/in.h stdlib.h string.h sys/ioctl.h sys/socket.h unistd.h sys/sockio.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STDBOOL
AC_C_CONST
AC_TYPE_PID_T

# Checks for library functions.
AC_FUNC_FORK
AC_CHECK_FUNCS([gethostbyname socket strchr strerror])

AC_OUTPUT( Makefile:Makefile.in )
