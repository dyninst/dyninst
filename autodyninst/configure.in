dnl Process this file with autoconf to produce a configure script.


AC_PREREQ(2.59)
AC_INIT(./autodyninst/configure.in)
AC_CONFIG_AUX_DIR(./autodyninst)
AC_PROG_CXX(g++)
AC_LANG(C++)


dnl === CONFIGURE OPTIONS


PACKAGE_BASE=""
AC_ARG_WITH(package-base,
	   [AS_HELP_STRING([--with-package-base],[Base directory to look for packages])],
	   [PACKAGE_BASE="${withval}";
            TCLTK_INC_DIR="${PACKAGE_BASE}/tcl8.4.5/include";
            TCLTK_LIB_DIR="${PACKAGE_BASE}/tcl8.4.5/lib";
            LIBELF_INC_DIR="${PACKAGE_BASE}/libelf/include";
            LIBELF_LIB_DIR="${PACKAGE_BASE}/libelf/lib";
            LIBDWARF_INC_DIR="${PACKAGE_BASE}/libdwarf/include";
            LIBDWARF_LIB_DIR="${PACKAGE_BASE}/libdwarf/lib";
            LIBXML2_INC_DIR="${PACKAGE_BASE}/libxml2/include/libxml2"
            LIBXML2_LIB_DIR="${PACKAGE_BASE}/libxml2/lib";
            LIBUNWIND_INC_DIR="${PACKAGE_BASE}/libunwind/include";
            LIBUNWIND_LIB_DIR="${PACKAGE_BASE}/libunwind/lib";
            LIBFLEX_LIB_DIR="${PACKAGE_BASE}/libflex/lib";
           ],[
            # We fall back on these defaults if both --with-package-base and
            # specific --with-<package> arguments are not provided.
            TCLTK_INC_DIR="/usr/include";
            TCLTK_LIB_DIR="/usr/lib";
            LIBELF_INC_DIR="/usr/include";
            LIBELF_LIB_DIR="/usr/lib";
            LIBDWARF_INC_DIR="/usr/include";
            LIBDWARF_LIB_DIR="/usr/lib";
            LIBXML2_INC_DIR="/usr/include/libxml2"
            LIBXML2_LIB_DIR="/usr/lib";
            LIBUNWIND_INC_DIR="/usr/include";
            LIBUNWIND_LIB_DIR="/usr/lib";
            LIBFLEX_LIB_DIR="/usr/lib";
           ])
# 3/15/09: We don't include libiberty in the cs.wisc.edu package-base.
#          Why?  Putting these here for now...
LIBIBERTY_INC_DIR="/usr/include";
LIBIBERTY_LIB_DIR="/usr/lib";


AC_ARG_WITH(tcl83,
            [AS_HELP_STRING([--with-tcl83],[Use Tcl/Tk 8.3])],
            [TCL_VERSION="tcl8.3"; USE_TCL83="${withval}";],
            [TCL_VERSION="tcl8.4"; USE_TCL83="false";])
AC_ARG_WITH(tcltk-incdir,
            [AS_HELP_STRING([--with-tcltk-incdir],[TCL/TK include directory])],
            [TCLTK_INC_DIR="${withval}";],)
AC_ARG_WITH(tcltk-libdir,
            [AS_HELP_STRING([--with-tcltk-libdir],[TCL/TK library directory])],
            [TCLTK_LIB_DIR="${withval}";],)
PD_SOFT_CHECK_INC_DIR($TCLTK_INC_DIR, [tcl.h])
PD_SOFT_CHECK_LIB_DIR($TCLTK_LIB_DIR, $TCL_VERSION, [Tcl_Eval])
AC_SUBST(USE_TCL83)
AC_SUBST(TCLTK_INC_DIR)
AC_SUBST(TCLTK_LIB_DIR)


AC_ARG_WITH(libelf-incdir,
            [AS_HELP_STRING([--with-libelf-incdir],[LIBELF include directory])],
            [LIBELF_INC_DIR="${withval}";],)
AC_ARG_WITH(libelf-libdir,
            [AS_HELP_STRING([--with-libelf-libdir],[LIBELF library directory])],
            [LIBELF_LIB_DIR="${withval}";],)
PD_CHECK_INC_DIR($LIBELF_INC_DIR, [libelf.h])
PD_CHECK_LIB_DIR($LIBELF_LIB_DIR,[elf],[elf_memory])
PD_CHECK_LIB_TYPE($LIBELF_LIB_DIR, elf)
LIBELF_STATIC="$LIB_TYPE_STATIC"
LIBELF_DYNAMIC="$LIB_TYPE_DYNAMIC"
AC_SUBST(LIBELF_INC_DIR)
AC_SUBST(LIBELF_LIB_DIR)
AC_SUBST(LIBELF_STATIC)
AC_SUBST(LIBELF_DYNAMIC)


AC_ARG_WITH(libdwarf-incdir,
            [AS_HELP_STRING([--with-libdwarf-incdir],[LIBDWARF include directory])],
            [LIBDWARF_INC_DIR="${withval}";],)
AC_ARG_WITH(libdwarf-libdir,
            [AS_HELP_STRING([--with-libdwarf-libdir],[LIBDWARF library directory])],
            [LIBDWARF_LIB_DIR="${withval}";],)
PD_CHECK_INC_DIR($LIBDWARF_INC_DIR,[libdwarf.h])
PD_CHECK_LIB_DIR($LIBDWARF_LIB_DIR,[dwarf],[dwarf_add_arange],$LIBDWARF_LIB_DIR,[-lelf])
PD_CHECK_LIB_TYPE($LIBDWARF_LIB_DIR, dwarf)
LIBDWARF_STATIC="$LIB_TYPE_STATIC"
LIBDWARF_DYNAMIC="$LIB_TYPE_DYNAMIC"
AC_SUBST(LIBDWARF_INC_DIR)
AC_SUBST(LIBDWARF_LIB_DIR)
AC_SUBST(LIBDWARF_STATIC)
AC_SUBST(LIBDWARF_DYNAMIC)


AC_ARG_WITH(libxml2-incdir,
            [AS_HELP_STRING([--with-libxml2-incdir],[LIBXML2 include directory])],
            [LIBXML2_INC_DIR="${withval}";],)
AC_ARG_WITH(libxml2-libdir,
            [AS_HELP_STRING([--with-libxml2-libdir],[LIBXML2 library directory])],
            [LIBXML2_LIB_DIR="${withval}";],)
PD_CHECK_INC_DIR($LIBXML2_INC_DIR, [libxml/xmlschemas.h])
PD_CHECK_LIB_DIR($LIBXML2_LIB_DIR,[xml2],[xmlAddID])
AC_SUBST(LIBXML2_INC_DIR)
AC_SUBST(LIBXML2_LIB_DIR)


AC_ARG_WITH(libiberty-incdir,
            [AS_HELP_STRING([--with-libiberty-incdir],[LIBIBERTY include directory])],
            [LIBIBERTY_INC_DIR="${withval}";],)
AC_ARG_WITH(libxml2-libdir,
            [AS_HELP_STRING([--with-libiberty-libdir],[LIBIBERTY library directory])],
            [LIBIBERTY_LIB_DIR="${withval}";],)
PD_SOFT_CHECK_INC_DIR($LIBIBERTY_INC_DIR,[libiberty.h])
PD_SOFT_CHECK_LIB_DIR($LIBIBERTY_LIB_DIR,[iberty],[cplus_demangle])
AC_SUBST(LIBIBERTY_INC_DIR)
AC_SUBST(LIBIBERTY_LIB_DIR)


AC_ARG_WITH(libunwind-incdir,
            [AS_HELP_STRING([--with-libunwind-incdir],[LIBUNWIND include directory])],
            [LIBUNWIND_INC_DIR="${withval}";],)
AC_ARG_WITH(libunwind-libdir,
            [AS_HELP_STRING([--with-libunwind-libdir],[LIBUNWIND library directory])],
            [LIBUNWIND_LIB_DIR="${withval}";],)
AC_CANONICAL_HOST
case $host in
     ia64-*-*-*) PD_CHECK_INC_DIR(${LIBUNWIND_INC_DIR}, [libunwind.h]) ;;
     *) ;;
esac
PD_SOFT_CHECK_INC_DIR($LIBUNWIND_INC_DIR,[unwind.h])
PD_SOFT_CHECK_LIB_DIR($LIBUNWIND_LIB_DIR,[unwind],[main])
AC_SUBST(LIBUNWIND_INC_DIR)
AC_SUBST(LIBUNWIND_LIB_DIR)


AC_ARG_WITH(libflex-libdir,
            [AS_HELP_STRING([--with-libflex-libdir],[Absolute path to flex library])],
            [LIBFLEX_LIB_DIR="${withval}";],)
# 3/15/09: We are not checking for libflex sources and they are not in cs.wisc.edu package-base.
#          What's the story here?  Not needed?
PD_CHECK_LIB_DIR(${LIBFLEX_LIB_DIR}, [fl], [yywrap])
AC_SUBST(LIBFLEX_LIB_DIR)
AC_PATH_PROG(NASM, nasm, "", "${PACKAGE_BASE}/nasm/bin:$PATH")
AC_SUBST(NASM)


AC_MSG_CHECKING([whether to use MM timer])
case $host in
     ia64-*-*-*) MMTIMER=true ;;
     *)	   MMTIMER=false ;;
esac
AC_MSG_RESULT(${MMTIMER})
AC_SUBST([MMTIMER])


AC_CHECK_FILE("${PACKAGE_BASE}/papi-3.0.0-test/papi.c",
	[PAPI_ROOT="${PACKAGE_BASE}/papi-3.0.0-test/"],
	[PAPI_ROOT=""])
AC_SUBST([PAPI_ROOT])


AC_CONFIG_FILES([make.config.local:./autodyninst/make.config.local.in])
AC_OUTPUT
